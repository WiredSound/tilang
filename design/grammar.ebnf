(* Groupings *)

<chunk> ::= (<stmt> newlines)* <stmt>

<block> ::= newlines indentincr <chunk> indentdecr

(* Statements *)

<stmt> ::= <if> | <while> | <function> | <declaration> | <assignment> | <return> | <display>

<if> ::= "if" <expr> <block>

<while> ::= "while" <expr> <block>

<function> ::= identifier "(" (<param> ("," <param>)*)? ")" ("->" typeidentifier)? <block>

<param> ::= typeidentifier identifier

<declaration> ::= typeidentifier identifier ("=" <expr>)?

<assignment> ::= identifier "=" <expr>

<return> ::= "return" <expr>?

<display> ::= "display" <expr>

(* Expressions *)

<expr> ::= <comparison> ("==" <comparison>)*

<comparison> ::= <addition> (("<"|">") <addition>)*

<addition> ::= <multiplication> (("+"|"-") <multiplication>)*

<multiplication> ::= <unary> (("*"|"/") <unary>)*

<unary> ::= ("!"|"~") <unary> | <primary>

<primary> ::= number | string | character | "true" | "false"
            | "(" <expr> ")" | identifier ("(" <exprs>? ")")?

<exprs> ::= <expr> ("," <expr>)*
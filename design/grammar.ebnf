(* Groupings *)

<chunk> ::= (<stmt> newlines)* <stmt>

<block> ::= newlines indentincr <chunk> indentdecr

(* Statements *)

<stmt> ::= <if> | <while> | <function> | <declaration> | <assignment>

<if> ::= "if" <expr> <block>

<while> ::= "while" <expr> <block>

<function> ::= identifier "(" (<param> ("," <param>)*)? ")" ("->" <type>)? <block>

<param> ::= <type> identifier

<type> ::= typeidentifier | "[" <type> "]"

<declaration> ::= <type> identifier ("=" <expr>)?

<assignment> ::= identifier "=" <expr>

(* Expressions *)

<expr> ::= <comparison> ("==" <comparison>)*

<comparison> ::= <addition> (("<"|">") <addition>)*

<addition> ::= <multiplication> (("+"|"-") <multiplication>)*

<multiplication> ::= <unary> (("*"|"/") <unary>)*

<unary> ::= ("!"|"~") <unary> | <primary>

<primary> ::= number | string | character | "true" | "false"
            | "[" <exprs>? "]" | "(" <expr> ")"
            | identifier ("(" <exprs>? ")")?

<exprs> ::= <expr> ("," <expr>)*
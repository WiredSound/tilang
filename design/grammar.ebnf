(* Groupings *)

<chunk> ::= (<stmt> newlines)*

<block> ::= newlines indentincr <chunk> indentdecr

(* Statements *)

<stmt> ::= <if> | <function> | <declaration> | <assignment>

<if> ::= "if" <expr> <block> ("else" <block>)?

<function> ::= identifier "(" (<param> ("," <param>)*)? ")" ("->" <type>)? <block>

<param> ::= <type> identifier

<type> ::= typeidentifier | "[" <type> "]"

<declaration> ::= <type> identifier ("=" <expr>)?

<assignment> ::= identifier "=" <expr>

(* Expressions *)

<expr> ::= <comparison> (("!="|"==") <comparison>)*

<comparison> ::= <addition> (("<"|">") <addition>)*

<addition> ::= <multiplication> (("+"|"-") <multiplication>)*

<multiplication> ::= <unary> (("*"|"/") <unary>)*

<unary> ::= ("!"|"~") <unary> | <primary>

<primary> ::= number | string | character | "true" | "false"
            | "[" (<expr> ("," <expr>)*)? "]"
            | "(" <expr> ")"
            | identifier
